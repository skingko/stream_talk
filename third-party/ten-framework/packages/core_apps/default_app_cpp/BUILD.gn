#
# This file is part of TEN Framework, an open source project.
# Licensed under the Apache License, Version 2.0.
# See the LICENSE file for more information.
#
import("//build/feature/ten_package.gni")
import("//build/ten_runtime/feature/publish.gni")
import("//build/ten_runtime/glob.gni")
import("//build/ten_runtime/options.gni")

ten_package("default_app_cpp") {
  package_kind = "app"

  resources = [
    "BUILD_release.gn.tent=>BUILD.gn.tent",
    "BUILD_release.gn=>BUILD.gn",
    "manifest.json",
    "manifest.json.tent",
    "property.json",
    "src/main.cc",
    "tools/run_script.py",
    "tools/run_script.py.tent",
  ]

  docs_files = exec_script("//.gnfiles/build/scripts/glob_file.py",
                           [
                             "--dir",
                             rebase_path("docs/**/*"),
                             "--dir-base",
                             rebase_path("docs"),
                             "--recursive",
                             "--only-output-file",
                           ],
                           "json")

  foreach(docs_file, docs_files) {
    docs_file_rel_path = docs_file.relative_path
    resources += [ "docs/${docs_file_rel_path}=>docs/${docs_file_rel_path}" ]
  }

  deps = []
  if (ten_enable_ten_manager) {
    deps +=
        [ "//core/src/ten_runtime:upload_ten_runtime_system_package_to_server" ]
  }
}

if (ten_enable_ten_manager) {
  ten_package_publish("upload_default_app_cpp_to_server") {
    base_dir = rebase_path("${root_out_dir}/app/default_app_cpp")
    deps = [ ":default_app_cpp" ]
  }
}
